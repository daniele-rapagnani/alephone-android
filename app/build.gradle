import java.nio.file.Paths

apply plugin: 'com.android.application'

android {
    ndkVersion gradle.ndkVersion
    
    compileSdkVersion 29
    buildToolsVersion "29.0.0"
    defaultConfig {
        aaptOptions {
            noCompress ''
            //ignoreAssetsPattern 'HD Monsters:HD Textures:HD Weapons:Enhanced HUD:TTEP v7'
        }

        applicationId "com.marathon.alephone"
        minSdkVersion 26
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++14"
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }
    }
    flavorDimensions "games"
    productFlavors {
        marathon {
            dimension "games"
            applicationId = "com.alephone.marathon"
        }
        marathon2 {
            dimension "games"
            applicationId = "com.alephone.marathon2"
        }
        marathonInfinity {
            dimension "games"
            applicationId = "com.alephone.marathoninfinity"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.14.4+"
        }
    }
    
    // I'll disable splits for now considering we are not
    // uploading the builds on the store.
    
    /*splits {
        abi {
            enable true
            reset()
            include "armeabi-v7a", "arm64-v8a"
        }
    }*/
}

task bakeAssetsList {
    android.productFlavors.all { flavor ->
        def paths = [];

        fileTree(project.file("src/${flavor.name}/assets")).visit {
            FileVisitDetails details ->
                paths.add(details.file);
        }

        def listFile = project.file("src/${flavor.name}/assets/__baked_fs");
        def assetsDir = project.file("src/${flavor.name}/assets/")

        if (listFile.exists()) {
            listFile.delete()
        }

        listFile << paths.size() << "\n"

        paths.each { File file ->
            listFile << file.getPath().replace(assetsDir.getAbsolutePath() + "/", "./") << "\n"
            listFile << (file.isDirectory() ? "D" : "F") << " "
            listFile << file.length() << "\n"
        }
    }
}

assemble.dependsOn(bakeAssetsList)

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'commons-io:commons-io:2.6'
    implementation 'com.google.android.material:material:1.1.0'

    def room_version = "2.3.0"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    implementation project(':SDL2')
    implementation project(':SDL_image')
    implementation project(':SDL_mixer')
    implementation project(':SDL_ttf')
    implementation project(':SDL_net')
    implementation 'androidx.recyclerview:recyclerview:1.2.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
